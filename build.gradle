plugins {
	id 'java'
    
    // https://github.com/diffplug/spotless
    id "com.diffplug.spotless" version "6.17.0"

    // https://graalvm.github.io/native-build-tools/latest/gradle-plugin.html
    id 'org.graalvm.buildtools.native' version '0.9.20' apply false
    // common plugins but don't apply at root
	// https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/
    id 'org.springframework.boot' version '3.0.4' apply false
    // https://docs.spring.io/dependency-management-plugin/docs/current/reference/html/   
	id 'io.spring.dependency-management' version '1.1.0' apply false
}
 
 repositories {
	mavenCentral()
}

 subprojects {
    apply plugin: "java"
    apply plugin: "com.diffplug.spotless"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.graalvm.buildtools.native"

    ext {
	    set('springBootAdminVersion', "3.0.2")
    }

    // https://github.com/diffplug/spotless/tree/main/plugin-gradle
    spotless {
        // https://github.com/diffplug/spotless/tree/main/plugin-gradle#java
        java {
            target 'src/*/java/**/*.java'

            importOrder()
            removeUnusedImports()
            indentWithSpaces()
            // https://github.com/diffplug/spotless/tree/main/plugin-gradle#google-java-format
            googleJavaFormat('1.11.0').aosp().reflowLongStrings()
    
            formatAnnotations()
        }
        // https://github.com/diffplug/spotless/tree/main/plugin-gradle#groovy
        groovyGradle {
            target '*.gradle', '**/*.gradle'
        }
        // https://github.com/prettier/plugin-xml
        format 'xml', {
            target 'src/**/*.xml'
            prettier(['prettier': '2.8.4', '@prettier/plugin-xml': '2.2.0']).config(['parser': 'xml'])
        }
        // https://github.com/diffplug/spotless/tree/main/plugin-gradle#yaml       
        yaml {
            target 'src/**/*.yml'
            jackson() 
        }    
    }

    configurations {
        springBom
    }

    repositories {
	    mavenCentral()
    }
    
    dependencies {
        // Logging logback included by default
        implementation 'org.springframework.boot:spring-boot-starter-logging'

       // pojo annotaions processing
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}